# Maintainer: mschae23 <pkg@mschae23.de>

pkgname=dearrow-cli
pkgver=3.4.1
pkgrel=1
pkgdesc='Program to view and vote for DeArrow submissions'
url='https://github.com/mschae23/dearrow-cli'
license=('AGPL-3.0-only' 'GPL-3.0-or-later' 'BSD-3-Clause' 'Apache-2.0' 'MIT')
makedepends=('cargo')
depends=('gcc-libs' 'glibc' 'openssl')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
source=("$pkgname-v$pkgver.tar.gz::https://github.com/mschae23/$pkgname/archive/v$pkgver.tar.gz" '0001-correct-license.patch')
sha512sums=('176b7afcabff0d5ca8840808d2147a1d586f7a17b8b2a88c681a7fd5855bb481bdda8eb56d33852ddd23f3e574034cfc8a152ee6433cd23f2e68f2ee6d0b8765' '31633690825550f574469ebccebdd2167968b965a6f9192a6b2fe6c6318286f5716c734957631d78a468787a2426874dcd2ee0e3f82734970b6107c1b2a66bdb')

prepare() {
    cd "$pkgname-$pkgver"
    # dearrow-cli's code itself is licensed under GPL v3, but the package as a whole is under AGPL v3
    patch -Np1 --fuzz=0 -i ../0001-correct-license.patch

    mkdir -p .cargo
    cat >> .cargo/config.toml << EOF
[registries.mschae23]
index = "sparse+https://mschae23.de/git/api/packages/mschae23/cargo/"
EOF

    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

check() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --frozen --all-features
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
    install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"
    install -Dm644 "CHANGELOG.md" "$pkgdir/usr/share/doc/${pkgname}/CHANGELOG.md"
}

